/********************************************************************************
ファイル名	：	Tanaoroshi.scp
バージョン	：	Ver.1.00
作成日付	：	2013/08/08
動作説明	：	棚卸登録
********************************************************************************/

Include "Lib\LogWrite"

//---------------------------------------------------
// メインメソッド
Method main()
Begin
	// --処理開始--
	JOB:job_main()
	
// スクリプトカスタマイズをする場合は下記の2行をコメントアウトするとデバッグ効率が良くなります
Catch
	Handy:ShowMessageBox("Applicationに問題が発生しました("&System:error&")", "ok" )
End Method

Package JOB
	m_btvCurrent = "sagyou1"	// カレント作業番号
	//---------------------------------------------------
	// メインメソッド
	Method job_main()
		
		btvRet = ""
	Begin
		
		While 1
			Common:Initialize()	
			Select Case m_btvCurrent
			Case "sagyou1"
				btvRet = SAGYOU1:sagyou_main()
			Case "sagyou2"
				btvRet = SAGYOU2:sagyou_main()
			End Select

			//ループ判定処理 (次ループ準備)
			If btvRet eq "returntop" Then 
				Wbreak
			ElseIf btvRet eq "retry" Then 
				m_btvCurrent = "sagyou2"
			ElseIf btvRet eq "back" Then 
				m_btvCurrent = "sagyou1"
			Else
				m_btvCurrent = btvRet
			EndIf
		Wend

		m_btvCurrent = "sagyou1"
		System:Load(System:arg1, 0, System:arg2, 0,0)
		Return (0)

	End Method
	
	// ログデータ作成・書き込み
	Method LogWrite(input)
	Begin
		
		If MatchCode:stockNo_match_NotP(input) is false Then
			Common:read_NG()
			Return () // 作業のリトライ
		EndIf
		
		ErrorCode:error_code = LogWrite:log_write_location(ConstString:FILE_TANAOROSHI, System:arg2, SAGYOU1:btvEachInput, input)
		If ErrorCode:error_code <> 0 Then
			Common:read_NG()
		Else
			Common:read_OK()
		EndIf
		
	End Method
	
EndPackage

/////////////////////////////////////////////////////////
//ロケーション入力に関連するPackage
/////////////////////////////////////////////////////////
Package SAGYOU1

	btvEachInput = ""

	Method sagyou_main()
		btvRet
		btvNextsagyou = "sagyou2"
	Begin
		
		disp_normal()
		While 1
			btvRet = wait_input()
			If btvRet eq "next" Then 
				If MatchCode:location_match(btvEachInput) is false Then
					Common:read_NG()
					Return ("back") // 作業のリトライ
				EndIf
				
				Common:read_OK()
				ErrorCode:dispDelete()
				Wbreak // 入力ループから抜ける
			ElseIf btvRet eq "back" Then 
				Return ("back") // 作業のリトライ
			Else
				Return (btvRet) // 作業 n へとぶ場合
			EndIf
		Wend
		
		Return (btvNextsagyou)
		
	End Method
	

	//---------------------------------------------------
	// 入力メソッド
	Method wait_input()
		btvKeycode
		btvRet
	Begin
		Common:BarcodeRead()
		
		With InputString
			:posx = 7 :posy = 6 :multiColumn = 1 :reverse = false :font = 1 
			:data = "" // 初期値設定
			:inputAreaSize = 13
			:maxLength = 12
			:minLength = 0
			:enableKeyInput = true
			:scanMode = 1
			:shift = 1
			:shiftPattern = "BT500"

			btvKeycode = :Exec()
			btvEachInput=:data
			
		EndWith

		// 遷移先返却メソッド呼び出し
		btvRet = InputLoop:input_loop(btvKeycode, JOB:m_btvCurrent)
		
		Return (btvRet)
	End Method
	
	//---------------------------------------------------
	// 通常入力画面 表示メソッド
	Method disp_normal()
	Begin
		With Screen
			:update = false
			:Clear()
			:foreColor = "255|255|255" :backColor = "0|0|128" :fontSize = "large"
			:posx = 1 :posy = 1 :multiColumn = 1 :reverse = false :font = 1 :OutputText("    棚卸登録    ")
			:foreColor = "0|0|0" :backColor = "255|255|255" :fontSize = "small"
			:posx = 1 :posy = 4 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" Location")
			:posx = 1 :posy = 6 :multiColumn = 1 :reverse = false :font = 1 :OutputText("    [              ]")
			:posx = 1 :posy = 18 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" F1:戻る   ENT:登録")

			:update = true
		EndWith
		ErrorCode:dispUpdate()
	End Method

EndPackage

/////////////////////////////////////////////////////////
//在庫NO入力に関連するPackage
/////////////////////////////////////////////////////////
Package SAGYOU2

	btvEachInput = ""

	Method sagyou_main()
		btvRet
	Begin
		
		disp_normal()
		
		While 1
			btvRet = wait_input()
			If btvRet eq "next" Then 
				JOB:LogWrite(btvEachInput)
				Wbreak // 入力ループから抜ける
			ElseIf btvRet eq "retry" Then 
				Return ("retry") // 作業のリトライ
			Else
				ErrorCode:dispDelete()
				Return (btvRet) // 作業 n へとぶ場合
			EndIf
		Wend
		
		Return ("retry")
		
	End Method
	

	//---------------------------------------------------
	// 入力メソッド
	Method wait_input()
		btvKeycode
		btvRet
	Begin
		
		Common:BarcodeRead()
		
		With InputString
			:posx = 7 :posy = 12 :multiColumn = 1 :reverse = false :font = 1 
			:data = "" // 初期値設定
			:inputAreaSize = 11
			:maxLength = 10
			:minLength = 0
			:enableKeyInput = true
			:scanMode = 1
			:shift = 1
			:shiftPattern = "BT500"

			btvKeycode = :Exec()
			btvEachInput=:data
			
		EndWith
		
		// 遷移先返却メソッド呼び出し
		btvRet = InputLoop:input_loop(btvKeycode, JOB:m_btvCurrent)
		
		Return (btvRet)
	End Method
	
	//---------------------------------------------------
	// 通常入力画面 表示メソッド
	Method disp_normal()
	Begin
		With Screen
			:update = false
			:Clear()
			:foreColor = "255|255|255" :backColor = "0|0|128" :fontSize = "large"
			:posx = 1 :posy = 1 :multiColumn = 1 :reverse = false :font = 1 :OutputText("    棚卸登録    ")
			:foreColor = "0|0|0" :backColor = "255|255|255" :fontSize = "small"
			:posx = 1 :posy = 4 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" Location")
			:posx = 1 :posy = 6 :multiColumn = 1 :reverse = false :font = 1 :OutputText("    [              ]")
			:posx = 7 :posy = 6 :multiColumn = 1 :reverse = false :font = 1 :OutputText(SAGYOU1:btvEachInput)
			:posx = 1 :posy = 8 :multiColumn = 1 :reverse = false :font = 1 :OutputText("          件数:")
			:posx = 17 :posy = 8 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Common:data_count(ConstString:FILE_TANAOROSHI))
			:posx = 1 :posy = 10 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" Box/Tag")
			:posx = 1 :posy = 12 :multiColumn = 1 :reverse = false :font = 1 :OutputText("    [            ]")
			:posx = 1 :posy = 14 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" 前回")
			:posx = 10 :posy = 14 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Common:get_last_data(ConstString:FILE_TANAOROSHI))
			:posx = 1 :posy = 18 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" F1:戻る   ENT:登録")
			:update = true
		EndWith
		ErrorCode:dispUpdate()
	End Method

EndPackage

