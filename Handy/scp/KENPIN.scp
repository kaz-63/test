/********************************************************************************
ファイル名	：	KENPIN.scp
バージョン	：	Ver.1.00
作成日付	：	2018/10/18
動作説明	：	入荷検品登録

********************************************************************************/

Include "Lib\Language"
Include "Lib\Message"
Include "Lib\Common"
Include "Lib\Utility"
Include "Lib\CustomWrite"

//---------------------------------------------------
// メインメソッド
Method main()
Begin
	
	// 言語設定
	Language:Initialize(System:arg2, ConstString:SCP_NAME_KENPIN)
	// メッセージ設定
	Message:Initialize(System:arg2, ConstString:SCP_NAME_KENPIN)
	
	// --処理開始--
	JOB:job_main()
	
// スクリプトカスタマイズをする場合は下記の2行をコメントアウトするとデバッグ効率が良くなります
Catch
	Handy:ShowMessageBox("Application Error(" & System:error & ")", "ok" )
End Method

Package JOB
	m_btvCurrent = "sagyou1"	// カレント作業番号
	//---------------------------------------------------
	// メインメソッド
	Method job_main()
		btvRet = ""
	Begin
		
		While 1
			Select Case m_btvCurrent
			Case "sagyou1"
				Common:Initialize()	
				btvRet = SAGYOU1:sagyou_main()
			Case "sagyou2"
				btvRet = SAGYOU2:sagyou_main()
			End Select

			//ループ判定処理 (次ループ準備)
			If btvRet eq "returntop" Then 
				Wbreak
			ElseIf btvRet eq "retry" Then 
				m_btvCurrent = "sagyou2"
			ElseIf btvRet eq "back" Then 
				m_btvCurrent = "sagyou1"
			Else
				m_btvCurrent = btvRet
			EndIf
		Wend
		
		// m_btvCurrentのリセット
		m_btvCurrent = "sagyou1"
		
		System:Load(System:arg1, 0, System:arg2, System:arg3, 0)
		Return (0)

	End Method
	
	/*!
	 * @brief ログデータ作成・書き込み
	 * 
	 * @param InputData 入力内容
	 * @return 遷移先を返す("back","retry")
	 */
	Method LogWrite(InputData)
	Begin
	
		If MatchCode:nyukaCountMatch(InputData) is false Then
			Common:read_NG()
			Return ("retry")
		EndIf
	
		ErrorCode:error_code = WriteLine:LogWriteKenpin(ConstString:KENPIN_DATA, SAGYOU1:btvEachInput, InputData, System:arg3)
		If ErrorCode:error_code <> 0 Then
			Common:read_NG()
		Else
			Common:read_OK()
		EndIf
		
		Return ("back")
		
	End Method
	
	Method disp_update()
	Begin
		
		With Screen
			:update = false
			:posx = 1 :posy = 17 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" "&ErrorCode:ErrorString(ErrorCode:error_code))
			:update = true
		EndWith
	End Method
	
EndPackage

/////////////////////////////////////////////////////////
//入荷検品入力に関連するPackage
/////////////////////////////////////////////////////////
Package SAGYOU1
	// 現品票記載の手配No.の桁数(10桁)
	Const TEHAI_NO_SIZE_GENPIN_10 = 10
	// 現品票記載の手配No.の桁数(12桁)
	Const TEHAI_NO_SIZE_GENPIN_12 = 12

	btvEachInput = ""

	Method sagyou_main()
		btvRet
		btvNextsagyou = "sagyou2"
		deleteMsg[2]
	Begin
		
		disp_normal()
		While 1
			btvRet = wait_input()
			If btvRet eq "next" Then
				// 現品票手配No.が10桁, 12桁の場合、先頭8桁を手配No.として採用する
				If btvEachInput.length == TEHAI_NO_SIZE_GENPIN_10 or btvEachInput.length == TEHAI_NO_SIZE_GENPIN_12 Then
					btvEachInput = btvEachInput.Left(ConstString:TEHAI_NO_SIZE)
				EndIf
				
				// 手配No.検証
				If MatchCode:tehaiMatch(btvEachInput) is false Then
					Common:read_NG()
					Return ("back") // 作業のリトライ
				EndIf
				
				Common:read_OK()
				ErrorCode:dispDelete()
				Wbreak // 入力ループから抜ける
			ElseIf btvRet eq "back" Then 
				Return ("back") // 作業のリトライ
			Else
				Return (btvRet) // 作業 n へとぶ場合
			EndIf
		Wend
		
		// 登録数が上限の場合、エラーウィンドウを出す
		If Common:dataCount(ConstString:KENPIN_DATA) == 999 Then
			deleteMsg = Message:MessageItem[24].split("{0}")
			Handy:ShowMessageBox(deleteMsg[0] & "\n" & deleteMsg[1], "ok" )
			Return ("back") // 作業のリトライ
		EndIf
		
		Return (btvNextsagyou)
		
	End Method
	
	//---------------------------------------------------
	// 入力メソッド
	Method wait_input()
		btvKeycode
		btvRet
	Begin
		Common:BarcodeRead()
		
		With InputString
			:Initialize()
			:posx = 3 :posy = 7 :multiColumn = 1 :reverse = false :font = 1 
			:data = ""									// 初期値設定
			:inputAreaSize = TEHAI_NO_SIZE_GENPIN_12 + 1
			:maxLength = TEHAI_NO_SIZE_GENPIN_12		// 現品票手配No.桁数が最大で12桁
			:minLength = 0
			:enableKeyInput = true
			:scanMode = 1
			:shift = 1
			:shiftPattern = "BT500"
			
			btvKeycode = :Exec()
			btvEachInput=:data
			
		EndWith
		
		// 遷移先返却メソッド呼び出し
		btvRet = InputLoop:input_loop(btvKeycode, JOB:m_btvCurrent)
		
		Return (btvRet)
	End Method
	
	//---------------------------------------------------
	// 通常入力画面 表示メソッド
	Method disp_normal()
	Begin
		With Screen
			:update = false
			:Clear()
			:fontSize = 12
			:posx = 1 :posy = 1 :multiColumn = 1 :reverse = true :font = 1 :OutputText(Language:langItem[0])
			:fontSize = "small"
			:posx = 1 :posy = 5 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[1])
			:posx = 1 :posy = 7 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" [" & " ".Repeat(TEHAI_NO_SIZE_GENPIN_12 + 1) & "]")
			:posx = 1 :posy = 13 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[3] & " " & Common:lastData(ConstString:KENPIN_DATA))
			:posx = 1 :posy = 19 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[4])
			CommonDisp:fillBlack("12", 0, 21)
			:update = true
		EndWith
		
		JOB:disp_update()
	End Method

EndPackage

/////////////////////////////////////////////////////////
//入荷数入力に関連するPackage
/////////////////////////////////////////////////////////
Package SAGYOU2

	btvEachInput = ""

	Method sagyou_main()
		btvRet
	Begin
		
		disp_normal()
		
		While 1
			btvRet = wait_input()
			If btvRet eq "next" Then
				Return (JOB:LogWrite(btvEachInput))
			ElseIf btvRet eq "retry" Then 
				Return ("retry") // 作業のリトライ
			Else
				ErrorCode:dispDelete()
				Return (btvRet) // 作業 n へとぶ場合
			EndIf
		Wend
		
	End Method
	
	//--------------------------------------------------
	// 入力メソッド
	Method wait_input()
		btvKeycode
		btvRet
	Begin
		
		With InputString
			:Initialize()
			:posx = 3 :posy = 11 :multiColumn = 1 :reverse = false :font = 1
			:inputAreaSize = 7
			:maxLength = 6
			:minLength = 0
			:enableKeyInput = true
			:scanMode = nil
			:shift = nil

			btvKeycode = :Exec()
			btvEachInput=:data
			
		EndWith
		
		// 遷移先返却メソッド呼び出し
		btvRet = InputLoop:input_loop(btvKeycode, JOB:m_btvCurrent)
		
		Return (btvRet)
	End Method
	
	//---------------------------------------------------
	// 通常入力画面 表示メソッド
	Method disp_normal()
	Begin
		With Screen
			:update = false
			:Clear()
			:fontSize = 12
			:posx = 1 :posy = 1 :multiColumn = 1 :reverse = true :font = 1 :OutputText(Language:langItem[0])
			:fontSize = "small"
			:posx = 1 :posy = 5 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[1])
			:posx = 1 :posy = 7 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" [" & SAGYOU1:btvEachInput & " ".Repeat(SAGYOU1:TEHAI_NO_SIZE_GENPIN_12 + 1 - ConstString:TEHAI_NO_SIZE) & "]")
			:posx = 1 :posy = 9 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[2])
			:posx = 1 :posy = 11 :multiColumn = 1 :reverse = false :font = 1 :OutputText(" [       ]")
			:posx = 1 :posy = 13 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[3] & " " & Common:lastData(ConstString:KENPIN_DATA))
			:posx = 1 :posy = 19 :multiColumn = 1 :reverse = false :font = 1 :OutputText(Language:langItem[4])
			CommonDisp:fillBlack("12", 0, 21)
			:update = true
		EndWith
		
		JOB:disp_update()
	End Method

EndPackage

